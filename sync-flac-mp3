#!/usr/bin/env perl

use strict;
use warnings;

use POSIX qw(nice);
use FindBin qw($Bin);
use File::Find;
use File::Path qw(make_path);
use File::Basename;
use Parallel::Forker;

# Please configure these:
my $flacpath = '/Volumes/ext/flac';
my $mp3path = '/Volumes/ext/flac-mp3';
my $max_proc = 2; # maximum level of parallel jobs to run for transcoding

# possibly interesting:
$ENV{LC_ALL}='en_US.UTF-8';
my $nicelevel = 10;

# shouldn't need to configure these:
my $flac2mp3 = "$Bin/flac2mp3";
my $min_mp3_size = 24; # theoretical minimum size of an mp3 frame (bytes)

die "$flacpath does not exist" unless -e $flacpath;
die "$mp3path does not exist" unless -e $mp3path;

my %existing_mp3s;
find(sub {
  $existing_mp3s{$File::Find::name} = 1
    if m/\.mp3$/i && ((stat $File::Find::name)[7] > $min_mp3_size);
}, $mp3path);

my @todo;
find(sub {
  if (/\.flac$/) {
  my $mp3 = $File::Find::name;
  $mp3 =~ s/\.flac$/\.mp3/;
  $mp3 =~ s/$flacpath/$mp3path/;
  push @todo, [$File::Find::name, $mp3]
    unless $existing_mp3s{$mp3};
  }
}, $flacpath);

print scalar(keys %existing_mp3s) . " existing mp3s\n";
print scalar(@todo) . " new FLAC files to convert\n";

my $forker = new Parallel::Forker (use_sig_child=>0);
$forker->max_proc($max_proc);
$SIG{TERM} = sub { $forker->kill_tree_all('TERM') if $forker && $forker->in_parent; die "Quitting...\n"; };
foreach my $pair (@todo) {
  make_path(dirname($pair->[1]));
  $forker->schedule(
    run_on_start => sub {
      $SIG{ALRM} = 'DEFAULT';
      $SIG{CHLD} = 'DEFAULT';
      nice($nicelevel);
      exec $flac2mp3, @$pair;
    },
    run_on_finish => sub {
      print "$pair->[1] done\n";
    },
  )->ready;

}

while ($forker->is_any_left) {
  $forker->poll;
  sleep 1;
}
print "DONE\n";
